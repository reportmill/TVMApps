
plugins {
    id 'application'
    id 'idea'
}

repositories {
    mavenCentral()

    // This is for TeaVM official release
    //maven { url = 'https://repo.maven.apache.org/maven2' }
}

project.ext.teavmVersion = "0.7.0-SNAPSHOT"

dependencies {

    // This artifact has TeaVM runtimes and tools
    //compile group: 'org.teavm', name: 'teavm-cli', version: '0.6.1'

    // Use teavm-cli jar from local Github build:
    //    cmd> git clone https://github.com/konsoletyper/teavm.git; cd teavm
    //    cmd> git checkout d710870c65fb3f8b0700b5d5fbf39292cc7e9a7f (3/31/21) (optional - get specific release)
    //    cmd> mvn clean install -DskipTests -Dteavm.build.all=false
    //    cmd> cd tools/cli; mvn clean install
    compile files('/Users/jeff/Dev/teavm/teavm-cli-0.7.0-SNAPSHOT.jar')

    // Referenced projects
    compile project(':SnapKit')
    compile project(':SnapTea')
    compile project(':SnapCharts')
    compile project(':SnapDemos')
    compile project(':SnapBuilder')
    compile project(':RMDraw')
    compile project(':RM16')
}

group = 'tvmapps'
version = '1.0-SNAPSHOT'

buildDir = 'build'

// MainClassNames = tvmapps.TVSnapCharts, tvmapps.TVRMDraw, tvmapps.TVRM16, tvmapps.TVSnapBuilder
mainClassName = 'tvmapps.TVSnapCharts'

ext.TV_OutDir = '/tmp/tvmout'

/**
 * Compile and generate resources and html.
 */
task tv_all {
    dependsOn 'tv_compile'
    dependsOn 'tv_copy_resources'
    dependsOn 'tv_gen_index_html'
}

/**
 * Run TeaVM compiler.
 */
task tv_compile (type: JavaExec){
    description = "Run TeaVM compiler"
    main = 'org.teavm.cli.TeaVMRunner'
    args = [ "-g", "-G", mainClassName, "-d", TV_OutDir ]
    maxHeapSize = '500m'
    classpath = sourceSets.main.runtimeClasspath
}

/**
 * Run TeaVM compiler.
 */
task tv_min (type: JavaExec){
    description = "Run TeaVM compiler"
    main = 'org.teavm.cli.TeaVMRunner'
    args = [ "-m", mainClassName, "-d", TV_OutDir ]
    classpath = sourceSets.main.runtimeClasspath
}

/**
 * Copies resource files from dependent projects to TeaVM output directory.
 */
task tv_copy_resources {

    doLast {

        // Get list of dependencies projects
        Set <ProjectDependency> projDeps = configurations.compile.allDependencies.findAll { it instanceof ProjectDependency }
        Set <Project> projs = projDeps.collect { it.dependencyProject }

        // Copy resources from dependencies projects to
        Set <File> rsrDirs = projs.collectMany { it.sourceSets.main.resources.srcDirs }
        rsrDirs.each { myFile ->
            println "copyRes " + myFile + ", " + myFile.getClass()
            copy {
                from myFile
                into TV_OutDir
                exclude '**/*.java'
                includeEmptyDirs false
            }
        }

        // Get all resource filenames, relative to TeaVM output directory
        List<String> txtFiles = rsrDirs.collectMany {
            String path = it.getAbsolutePath()
            List<String> paths = new FileNameFinder().getFileNames(path, "" /* includes */, '**/*.java' /* excludes */)
            paths.collect { it.substring(path.length()) }
        }

        // Write resource filenames to index.txt file in TeaVM output directory
        File file = file(TV_OutDir + "/index.txt")
        file.write("")
        txtFiles.each { file.append(it); file.append('\n') }
    }
}

/**
 * Generates the TeaVM index.html file in TeaVM output directory.
 */
task tv_gen_index_html {

    doLast {

        File file = file(TV_OutDir + "/index.html")
        file.write(TV_HTML_Text)
    }
}

ext.TV_HTML_Text = '''<!DOCTYPE html>
<html>
  <head>
    <title>Main page</title>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8">
    <script type="text/javascript" charset="utf-8" src="classes.js"></script>
  </head>
  <body onload="main()">
    <!-- TODO: add HTML content -->
  </body>
</html>
'''