import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'war'
    id 'org.teavm' version '0.13.0'
}

group 'com.reportmill'
version  new SimpleDateFormat("yyyy.MM").format(new Date())

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {

    // TeaVM
    implementation teavm.libs.jsoApis

    // Referenced projects
    implementation project(':SnapKit')
    implementation project(':SnapTea')
    implementation project(':SnapDemos')
}

teavm {

    all {
        mainClass = "tvmapps.TVSlideShow"
        outputDir = file ("$buildDir" + "/libs")
        //optimization = org.teavm.gradle.api.OptimizationLevel.NONE
    }
    js {
        addedToWebApp = true
        //obfuscated = false

        // this is also optional, default value is <project name>.js
        targetFileName = "classes.js"
    }
}

// Output directory
ext.TV_OutDir = '/tmp/tvmout'

/**
 * Copies resource files from dependent projects to TeaVM output directory.
 */
tasks.register('tv_copy_resources') {

    doLast {

        // Get list of dependencies projects
        DependencySet allDependencies = configurations.implementation.allDependencies
        Set<ProjectDependency> projDeps = allDependencies.findAll { dep -> dep instanceof ProjectDependency }
        Set<Project> projs = projDeps.collect { projDep -> projDep.dependencyProject }

        // Copy resources from dependencies projects to
        List<File> rsrDirs = projs.collectMany { it.sourceSets.main.resources.srcDirs }
        rsrDirs.each { myFile ->
            println "copyRes " + myFile + ", " + myFile.getClass()
            copy {
                from myFile
                into TV_OutDir
                exclude '**/*.java'
                includeEmptyDirs false
            }
        }

        // Get all resource filenames, relative to TeaVM output directory
        List<String> rsrcFileNames = rsrDirs.collectMany { rsrcDir ->
            String path = rsrcDir.getAbsolutePath()
            FileTree files = fileTree(rsrcDir).matching { exclude '**/*.java' }
            files.collect {file -> file.getAbsolutePath().substring(path.length()) }
        }
        Collections.sort(rsrcFileNames)

        // Write resource filenames to index.txt file in TeaVM output directory
        File file = file(TV_OutDir + "/index.txt")
        file.write("")
        rsrcFileNames.forEach(rsrcFile -> { file.append(rsrcFile); file.append('\n'); })
    }
}

/**
 * Generates the TeaVM index.html file in TeaVM output directory.
 */
tasks.register('tv_gen_index_html') {

    doLast {

        File file = file(TV_OutDir + "/index.html")
        file.write(TV_HTML_Text)
    }
}

ext.TV_HTML_Text = '''<!DOCTYPE html>
<html>
  <head>
    <title>Main page</title>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8">
    <script type="text/javascript" charset="utf-8" src="classes.js"></script>
  </head>
  <body onload="main()">
    <!-- TODO: add HTML content -->
  </body>
</html>
'''